:: first we import our /sur/ttt.hoon type definitions and expose them directly
::
/-  *ttt
:: our front-end takes in the bowl from our agent and also our agent's state
::
|=  [bol=bowl:gall gboard=boardmap gdims=dims] ::   =page  playmap=playerinfo]
:: 5. we return an $octs, which is the encoded body of the HTTP response and its byte-length
::
|^  ^-  octs
:: 4. we convert the cord (atom string) to an octs
::
%-  as-octs:mimes:html
:: 3. we convert the tape (character list string) to a cord (atom string) for the octs conversion
::
%-  crip
:: 2. the XML data structure is serialized into a tape (character list string)
::
%-  en-xml:html
:: 1. we return a $manx, which is urbit's datatype to represent an XML structure
::
^-  manx
=/  clist=(list (list coord))  (make-keys 3 3)

;html
  ;head
    ;title: tictactoe
    ;meta(charset "utf-8");
    ;style
      ;+  ;/  style
    ==  ::style
  ==  ::head
  ;body
    ;h1: Sample Tic-Tac-Toe Board:
    ;h2: ðŸ–™ Use console pokes to set moves.  Refresh the page to see the results.  See the structure file for more details. ðŸ–˜
    ;div.contain
      ;*  ?~  clist  !!
        %+  turn  clist
          |=  rclist=(list coord)
          ;div.board
            ;*
            ?~  rclist  !!
              %+  turn  rclist
              |=  rc=coord
                =/  value  (need (~(get by gboard) rc))
                =/  symbol  ?-  value
                        %o  "â­˜"
                        %e  "_"
                        %x  "â¨¯"
                      ==
                ;div.whitesquare: {symbol}
          == ::div outer
    ==  ::p
  == ::body
== ::html
:: A general reminder:  Use a mictar rune for each new %+ turn and sub-elements generated.
:: Using one mictar with multiple levels of loop and/or sub-elements leads to ruin.
++  make-keys 
  |=  [rmax=@ud cmax=@ud]
    ^-  (list (list coord))
    =/  row  0
    =/   llcord  `(list (list coord))`~
    |-
      ^-  (list (list coord))
      ?:  (lth row rmax)
        %=  $
          llcord  (snoc llcord (get-row row cmax))
          row  +(row)
        ==
        llcord
++  get-row
  |=  [row=@ud cmax=@ud]
    ^-  (list coord)
      =/  col  0
      =/  result  `(list coord)`~
      |- 
      ^-  (list coord)
       ?:  (lth col cmax)
         %=  $
            result  (snoc result [row col])
            col  +(col)
          ==
          result
++  style
  ^~
  %-  trip
    ::Board CSS generated by chatGPT 3.5.
    '''
    body {background-color:black; color: orange;}
    h1 {font-size: 36pt; text-align: center;}
    h2 {font-size: 24pt; text-align: center;}
    div {font-size: 16pt;}

    .contain {
      position:relative;
      left:20%;
    }


    .board {
      margin-top: 5px;
      display: grid;
      grid-template-columns: repeat(3, 500px);
      column-gap: 5px;

    }

    .square {
      width: 250px;
      height: 250px;
      background-color: orange;
      display: flex;
      align-items: center;
      justify-content: center;
      font-size: 48px;
      font-weight: bold;
      color: black;
      cursor: pointer;
    }

    .blacksquare {
      width: 500px;
      height: 250px;
      background-color: black;
      align-items: center;
      justify-content: center;
      font-size: 48px;
      font-weight: bold;
      color: white;
      cursor: pointer;
    }

    .whitesquare {
      width: 500px;
      height: 250px;
      background-color: orange;
      color: blue;
      font-size: 48px;
      display: flex;
      align-items: center;
      justify-content: center;
      font-weight: bold;
      cursor: pointer;
    }

    '''
--
